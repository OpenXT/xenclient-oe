#
# This is how bitbake will establish its configuration:
#
# require conf/abi_version.conf
# include conf/site.conf
# include conf/auto.conf
# include conf/local.conf
# include conf/build/${BUILD_SYS}.conf
# include conf/target/${TARGET_SYS}.conf
# include conf/machine/${MACHINE}.conf
# include conf/machine-sdk/${SDKMACHINE}.conf
# include conf/distro/${DISTRO}.conf
# include conf/distro/defaultsetup.conf
# include conf/documentation.conf
# include conf/licenses.conf
# require conf/sanity.conf
#

# openembedded-core sets a default list of DISTRO_FEATURES that do not match
# OpenXT's machine needs.
# See: openembedded-core/meta/conf/distro/include/default-distrovars.inc
# Another way would be hard setting DISTRO_FEATURES.
# Another way would be to redefine DISTRO_FEATURES_DEFAULT.
# Down the road this should probably be a machine config thing so it is possible
# to have images that do not include selinux
DISTRO_FEATURES_remove = "zeroconf nfs"
DISTRO_FEATURES_append += "pam selinux multiarch virtualization polkit opengl"

# Use OpenXT Linux patched kernel.
PREFERRED_PROVIDER_virtual/kernel = "linux-openxt"
PREFERRED_VERSION_linux-openxt ?= "5.10%"
# Align linux-libc-headers with the preferred kernel version.
PREFERRED_VERSION_linux-libc-headers ?= "${@"${PREFERRED_VERSION_linux-openxt}".rstrip('%')}"
PREFERRED_VERSION_linux-libc-headers-nativesdk ?= "${@"${PREFERRED_VERSION_linux-openxt}".rstrip('%')}"

# Dojosdk preferred version.
PREFERRED_VERSION_dojosdk-native ?= "1.7.12"
# ${MACHINE}.conf is overriden by ${DISTRO}.conf
# xenmgr_data and sync-wui (uivm and syncui machines) apparently depends on
# different versions, so preserve that.
PREFERRED_VERSION_dojosdk-native_xenclient-syncui ?= "1.8.1"

# Prefer openssl to libressl.
PREFERRED_PROVIDER_openssl = "openssl"
PREFERRED_PROVIDER_openssl-native = "openssl-native"
PREFERRED_PROVIDER_openssl-conf = "openssl-conf"
PREFERRED_PROVIDER_nativesdk-openssl = "nativesdk-openssl"
PREFERRED_PROVIDER_nativesdk-openssl-conf = "nativesdk-openssl-conf"
# Prefer libcrypto and libssl to alternatives.
PREFERRED_PROVIDER_libcrypto = "libcrypto"
PREFERRED_PROVIDER_libssl = "libssl"

# Prefer libx11 to libx11-diet.
PREFERRED_PROVIDER_virtual/libx11 = "libx11"

# Force specific package versions
PREFERRED_VERSION_networkmanager = "1.18.4"
PREFERRED_VERSION_network-manager-applet = "1.8.22"

PREFERRED_PROVIDER_iasl = "acpica"
PREFERRED_PROVIDER_iasl-native = "${PREFERRED_PROVIDER_iasl}-native"

PREFERRED_VERSION_refpolicy-mcs = "2.20190201"

# Select our preferred providers
PREFERRED_PROVIDER_sysvinit-inittab = "sysvinit"
PREFERRED_PROVIDER_virtual/java-initial = "cacao-initial"
PREFERRED_PROVIDER_virtual/java-initial-native = "cacao-initial-native"
PREFERRED_PROVIDER_virtual/java-native = "jamvm-native"
PREFERRED_PROVIDER_virtual/javac-native = "ecj-bootstrap-native"
PREFERRED_PROVIDER_jpeg-native = "jpeg-native"

# Set the preferred version to C xenstored from xen-tools
PREFERRED_PROVIDER_virtual/xenstored = "xen-tools"
PREFERRED_RPROVIDER_virtual/xenstored = "xen-tools-xenstored"
# Force exclusion of the OCaml xenstored since the above preferences
# are not sufficient to override that as the default selection:
PACKAGE_EXCLUDE += " xen-ocaml-libs-xenstored"

# The C and OCaml xenstored packages can be installed together
# and this whitelisting is necessary to allow both to be built
MULTI_PROVIDER_WHITELIST += " virtual/xenstored"

# Default to rsyslog recipe which RPROVIDES rsyslog-conf
# Dom0 and Installer install rsyslog-conf-dom0 instead.
PREFERRED_RPROVIDER_rsyslog-conf = "rsyslog"

# Enable tui for GDB by default. This makes it easier to debug small things
# using the gdb ipk.
PACKAGECONFIG_append_pn-gdb = " tui"

# rpcgen staging and install paths.
require xc-rpcgen.inc

FILESYSTEM_PERMS_TABLES = "files/openxt-fs-perms.txt"

# Declare the MACHINE type and KERNEL image name for stubdomains
STUBDOMAIN_MACHINE ?= "xenclient-stubdomain"
STUBDOMAIN_KERNEL ?= "bzImage"

# Set our root home
ROOT_HOME = "/root"
