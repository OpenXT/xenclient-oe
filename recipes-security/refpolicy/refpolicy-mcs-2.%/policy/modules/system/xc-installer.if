#############################################################################
#
# Copyright (C) 2014 Citrix Systems, Inc.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
#
#############################################################################

## <summary>XenClient installer and updater.</summary>

########################################
## <summary>
##	Execute the XenClient installer in the
##	XenClient installer domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
## TODO: Transition isn't based on a proper executable
##   type.  The files in the control.tar.gz need to be
##   labeled properly first.
#
interface(`xc_installer_domtrans',`
	gen_require(`
		type xc_installer_t, xc_installer_exec_t;
		type updatemgr_storage_t;
	')

	# required due to reference to external type
	#  will go away once entry point is labeled properly
	application_domain(xc_installer_t, updatemgr_storage_t)

	corecmd_search_bin($1)
	domtrans_pattern($1, updatemgr_storage_t, xc_installer_t)
	domtrans_pattern($1, xc_installer_exec_t, xc_installer_t)
	allow $1 xc_installer_t:process { noatsecure siginh rlimitinh };
')
########################################
## <summary>
##	Allow the calling domain to remove installer files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`xc_installer_delete',`
	gen_require(`
		type xc_installer_storage_t;
	')

	allow $1 xc_installer_storage_t:dir manage_dir_perms;
	allow $1 xc_installer_storage_t:file delete_file_perms;
')
########################################
## <summary>
##	Allow the calling domain to read installer tmp files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`xc_installer_read_tmp_files',`
	gen_require(`
		type xc_installer_tmp_t;
	')

	read_files_pattern($1, xc_installer_tmp_t, xc_installer_tmp_t)
')
########################################
## <summary>
##	Allow the calling domain to mount filesystem on all tmpfs directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`xc_installer_mounton_tmp_dirs',`
	gen_require(`
		type xc_installer_tmp_t;
	')

	allow $1 xc_installer_tmp_t:dir mounton;
')

