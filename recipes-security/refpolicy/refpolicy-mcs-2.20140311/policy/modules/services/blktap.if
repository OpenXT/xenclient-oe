#############################################################################
#
# Copyright (C) 2014 Citrix Systems, Inc.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
#
#############################################################################

## <summary>blktap module</summary>

########################################
## <summary>
##	Execute a domain transition to run tapdisk/tapdisk2 daemon.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed to transition.
## 	</summary>
## </param>
#
interface(`tapdisk_domtrans',`
	gen_require(`
		type tapdisk_t, tapdisk_exec_t;
	')

	domtrans_pattern($1, tapdisk_exec_t, tapdisk_t)
')
########################################
## <summary>
##	Execute a domain transition to run tap-ctl utility in the specified role.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed to transition.
## 	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to allowed in domain.
##	</summary>
## </param>
#
interface(`tapdisk_run',`
	gen_require(`
		type tapdisk_t, tapdisk_exec_t;
	')

	tapdisk_domtrans($1)
	role $2 types tapdisk_t;
')
########################################
## <summary>
##	List contents of blktap-control directory.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
## 	</summary>
## </param>
#
interface(`tapdisk_list_control_dir',`
	gen_require(`
		type tapdisk_var_run_t;
	')

	allow $1 tapdisk_var_run_t:dir list_dir_perms;
')
########################################
## <summary>
##	Read/Write blktap-control directory.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
## 	</summary>
## </param>
#
interface(`tapdisk_rw_control_dir',`
	gen_require(`
		type tapdisk_var_run_t;
	')

	allow $1 tapdisk_var_run_t:dir rw_dir_perms;
')
########################################
## <summary>
##	Read from blktap sockets.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
## 	</summary>
## </param>
#
interface(`tapdisk_read_sock',`
	gen_require(`
		type tapdisk_var_run_t;
	')

	allow $1 tapdisk_var_run_t:sock_file read_sock_file_perms;
')
########################################
## <summary>
##	Get attributes of blktap block devices.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
## 	</summary>
## </param>
#
interface(`blktap_getattr_blk_file',`
	gen_require(`
		type blktap_device_t;
	')

	allow $1 blktap_device_t:blk_file getattr_file_perms;
')
########################################
## <summary>
##	Relabel blktap block devices.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
## 	</summary>
## </param>
#
interface(`blktap_relabel_blk_file',`
	gen_require(`
		type blktap_device_t;
	')


	allow $1 blktap_device_t:blk_file { relabelfrom relabelto };
')
########################################
## <summary>
##	Read from blktap block devices.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
## 	</summary>
## </param>
#
interface(`blktap_read_blk_file',`
	gen_require(`
		type blktap_device_t;
	')

	allow $1 blktap_device_t:blk_file read_file_perms;
')
########################################
## <summary>
##	Read and write blktap block devices.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
## 	</summary>
## </param>
#
interface(`blktap_rw_blk_file',`
	gen_require(`
		type blktap_device_t;
	')

	allow $1 blktap_device_t:blk_file rw_file_perms;
')
########################################
## <summary>
##	Connect to tapdisk over unix domain stream socket.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed access.
## 	</summary>
## </param>
#
interface(`tapdisk_stream_connect',`
	gen_require(`
		type tapdisk_t, tapdisk_var_run_t;
	')

        stream_connect_pattern($1, tapdisk_var_run_t, tapdisk_var_run_t, tapdisk_t)
')
########################################
## <summary>
##	Execute a domain transition to run tap-ctl utility.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed to transition.
## 	</summary>
## </param>
#
interface(`tapctl_domtrans',`
	gen_require(`
		type tapctl_t, tapctl_exec_t;
	')

	domtrans_pattern($1, tapctl_exec_t, tapctl_t)
')
########################################
## <summary>
##	Execute a domain transition to run tap-ctl utility in the specified role.
## </summary>
## <param name="domain">
## 	<summary>
##	Domain allowed to transition.
## 	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to allowed in domain.
##	</summary>
## </param>
#
interface(`tapctl_run',`
	gen_require(`
		type tapctl_t, tapdisk_exec_t;
	')

	tapctl_domtrans($1)
	role $2 types tapctl_t;
	tapdisk_run($1, $2)
')
